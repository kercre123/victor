/** Clad message definitions for the messages sent from the WiFi to the engine.
 */

#include "clad/types/animationEvents.clad"

namespace Anki {
namespace Cozmo {

enum uint_8 AnimTrackFlag {
  NO_TRACKS = 0,
  HEAD_TRACK = 0x01,
  LIFT_TRACK = 0x02,
  BODY_TRACK = 0x04,
  FACE_IMAGE_TRACK = 0x08,
  EVENT_TRACK = 0x10,
  BACKPACK_LIGHTS_TRACK = 0x20,
  AUDIO_TRACK = 0x40,
  //RESERVED = 0x80,
  ALL_TRACKS = 0xFF
}

enum int_32 AnimConstants
{
  AUDIO_SAMPLE_RATE     = 22320,  // 7440 Hz (Robot clock speed) * 3
  AUDIO_SAMPLE_SIZE     = 744,    // (1 sec / 30 frames) * 22,320 Sample Rate = 744 Audio Samples
  MAX_FACE_FRAME_SIZE   = 1024,
  KEYFRAME_BUFFER_SIZE  = 16384,
  NUM_TRACKS            = 8
}

namespace AnimKeyFrame {

  message AudioSample
  {
    uint_8 sample[744] // AUDIO_SAMPLE_SIZE above
  }

  message AudioSilence
  { // Must not contain any members
  }

  message HeadAngle
  {
    uint_16 time_ms,
    int_8   angle_deg,
  }

  message LiftHeight
  {
    uint_16 time_ms,
    uint_8  height_mm,
  }

  message FacePosition
  {
    int_8 xCen,
    int_8 yCen,
  }
  
  message Blink
  {
    bool enable,
    bool blinkNow,
  }
  
  message FaceImage
  {
    uint_8 image[uint_16:1024], // MAX_FACE_FRAME_SIZE above
  }
  
  message BackpackLights
  {
    uint_16 colors[5],
  }
  
  message BodyMotion
  {
    int_16 speed,              // Commanded speed in mm/sec or deg/sec
    int_16 curvatureRadius_mm, // +ve: curves left, -ve: curves right, ...
                               // ...u16_MAX/MIN: drive straight...
                               // ...0: turn in place (+ve speed: left, -ve speed: right)
  }

  message Event
  {
    AnimEvent event_id
  }

  message EnableAnimTracks
  {
    uint_8 whichTracks
  }

  message DisableAnimTracks
  {
    uint_8 whichTracks
  }

  message InitController
  {
  }

  message StartOfAnimation
  {
    uint_8 tag
  }

  message EndOfAnimation
  {
  }

} // namespace AnimKeyFrame

} // namespace Cozmo
} // namespace Anki
