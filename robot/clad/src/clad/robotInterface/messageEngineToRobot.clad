/** Clad definitions of all messages sent from the Cozmo engine to the robot and from one part of the robot to another.
 * This file name is now out of date since it should be messageToRobot since messages may not be coming from the engine.
 *
 * Used in the engine, all three robot processors and the test fixture, however, we don't have to build and flash in 
 * lockstep because of strict tag space partitioning.
 */
 
#include "clad/types/imageTypes.clad"
#include "clad/types/robotStatusAndActions.clad"
#include "clad/types/ledTypes.clad"
#include "clad/types/animationKeyFrames.clad"
#include "clad/types/robotTestModes.clad"
#include "clad/types/dockingSignals.clad"
#include "clad/types/imu.clad"
#include "clad/robotInterface/messageToActiveObject.clad"
#include "clad/robotInterface/otaMessages.clad"
#include "clad/types/nvStorage.clad"

namespace Anki {
namespace Cozmo {
namespace RobotInterface {

/** Pipe from engine to robot.
 * This cannot be an auto union because we need explicit tag values to partition the RTIP and WiFi code
 * and to make debugging from message dumps more sane.
 */

message DriveWheels
{
  float_32 lwheel_speed_mmps,
  float_32 rwheel_speed_mmps,
}

message DriveWheelsCurvature
{
  int_16  speed_mmPerSec,      // Commanded speed in mm/sec
  uint_16 accel_mmPerSec2,     // Commanded max absolute value of...
  uint_16 decel_mmPerSec2,      // ...acceleration/deceleration in mm/sec^2
  int_16  curvatureRadius_mm   // +ve: curves left, -ve: curves right, ...
                               // ...u16_MAX: point turn left, 16_MIN: point ...
                               // ...turn right, 0: straight
}

message MoveLift
{
  float_32 speed_rad_per_sec,
}

message MoveHead
{
  float_32 speed_rad_per_sec,
}

message SetLiftHeight
{
  float_32 height_mm,
  float_32 max_speed_rad_per_sec,
  float_32 accel_rad_per_sec2,
  float_32 duration_sec,
}

message SetHeadAngle
{
  float_32 angle_rad,
  float_32 max_speed_rad_per_sec,
  float_32 accel_rad_per_sec2,
  float_32 duration_sec,
}

message HeadAngleUpdate 
{
  float_32 newAngle,
}

// This is a turn in place to an absolute angle
message SetBodyAngle
{
  float_32 angle_rad,
  float_32 max_speed_rad_per_sec,
  float_32 accel_rad_per_sec2
}

message TurnInPlaceAtSpeed
{
  float_32 speed_rad_per_sec,
  float_32 accel_rad_per_sec2,
}

message StopAllMotors
{
}

message ClearPath
{
  uint_16 pathID,
}

message PathSegmentSpeed
{
  float_32 target,
  float_32 accel,
  float_32 decel,
}

message AppendPathSegmentLine
{
  float_32 x_start_mm,
  float_32 y_start_mm,
  float_32 x_end_mm,
  float_32 y_end_mm,
  PathSegmentSpeed speed,
}

message AppendPathSegmentArc
{
  float_32 x_center_mm,
  float_32 y_center_mm,
  float_32 radius_mm,
  float_32 startRad,
  float_32 sweepRad,
  PathSegmentSpeed speed,
}

message AppendPathSegmentPointTurn
{
  float_32 x_center_mm,
  float_32 y_center_mm,
  float_32 targetRad,
  PathSegmentSpeed speed, // targetSpeed indicates rotational speed in rad/s
  bool     useShortestDir
}

message TrimPath
{
  uint_8 numPopFrontSegments,
  uint_8 numPopBackSegments,
}

message ExecutePath
{
  uint_16 pathID,
  bool    useManualSpeed,
}

message AbsoluteLocalizationUpdate
{
  uint_32 timestamp,
  uint_32 pose_frame_id,
  float_32 xPosition,
  float_32 yPosition,
  float_32 headingAngle,
}

message SyncTime
{
  uint_32 robotID,
  uint_32 syncTime,
}

message ImageRequest
{
  ImageSendMode  sendMode,
  ImageResolution resolution,
}

enum uint_8 ControllerChannel
{
  controller_wheel,
  controller_head,
  controller_lift,
  controller_steering
}

message ControllerGains
{
  float_32 kp,
  float_32 ki,
  float_32 kd,
  float_32 maxIntegralError,
  ControllerChannel controller
}

message SetMotionModelParams
{
  float_32 slipFactor,
}

message BackpackLights
{
  LightState lights[5],
}

message AbortAnimation
{
}

message EnablePickupParalysis
{
  bool enable
}

message EnableLiftPower
{
  bool enable
}

message RTIPVersionInfo
{
  uint_32 version,
  uint_32 date,
  string  description
}

enum uint_32 ToRobotAddressSpace
{
  GLOBAL_INVALID_TAG = 0,
  TO_BODY_START = 0x01,
  TO_BODY_END   = 0x2F,
  TO_RTIP_START = 0x30,
  TO_RTIP_END   = 0x7F,
  TO_WIFI_START = 0x80,
  TO_WIFI_END   = 0xAF,
  TO_ENG_START  = 0xB0,
  TO_ENG_UNREL  = 0xF0,
  TO_ENG_END    = 0xFE
}

message BodyUpgradeData
{
  uint_32 word
}

message RadioState
{
  bool wifiConnected
}

/// *************** Overall message pipe ******************************* ///

/** Pipe from engine to robot and within the robot
 * This cannot be an auto union because we need explicit tag values to partition the messages between the engine (not
 * included in this file), WiFi, RTIP, body and test fixture. And significantly to make debugging from message dumps
 * more sane.
 */
union EngineToRobot
{
  // 0x00 Is invalid message
  
  // 0x01 to 0x2f: Messages to the body / test fixture
  BootloadBody                  bootloadBody                = 0x01,
  BodyUpgradeData               bodyUpgradeData             = 0x02,
  
  // 0x30 to 0x7f: To RTIP
  BootloadRTIP                  bootloadRTIP                = 0x30,
  RadioState                    radioConnected              = 0x31,
  DriveWheels                   drive                       = 0x32,
  DriveWheelsCurvature          driveCurvature              = 0x33,
  MoveLift                      moveLift                    = 0x34,
  MoveHead                      moveHead                    = 0x35,
  SetLiftHeight                 liftHeight                  = 0x36,
  SetHeadAngle                  headAngle                   = 0x37,
  HeadAngleUpdate               headAngleUpdate             = 0x38,
  SetBodyAngle                  setBodyAngle                = 0x39,
  TurnInPlaceAtSpeed            turnInPlaceAtSpeed          = 0x3A,
  StopAllMotors                 stop                        = 0x3B,
  ClearPath                     clearPath                   = 0x3C,
  AppendPathSegmentLine         appendPathSegLine           = 0x3D,
  AppendPathSegmentArc          appendPathSegArc            = 0x3E,
  AppendPathSegmentPointTurn    appendPathSegPointTurn      = 0x3F,
  TrimPath                      trimPath                    = 0x40,
  ExecutePath                   executePath                 = 0x41,
  DockWithObject                dockWithObject              = 0x42,
  AbortDocking                  abortDocking                = 0x43,
  PlaceObjectOnGround           placeObjectOnGround         = 0x44,
  AbsoluteLocalizationUpdate    absLocalizationUpdate       = 0x45,
  StartControllerTestMode       startControllerTestMode     = 0x46,
  ControllerGains               setControllerGains          = 0x47,
  DockingErrorSignal            dockingErrorSignal          = 0x48,
  CarryStateUpdate              setCarryState               = 0x49,
  ImuRequest                    imuRequest                  = 0x4A,
  SyncTime                      syncTime                    = 0x4B,
  ImageRequest                  imageRequest                = 0x4C,
  BackpackLights                setBackpackLights           = 0x4D,
  CubeLights                    setCubeLights               = 0x4E,
  FlashObjectIDs                flashObjectIDs              = 0x4F,
  ObjectBeingCarried            setObjectBeingCarried       = 0x50,
  EnablePickupParalysis         enablePickupParalysis       = 0x51,
  EnableLiftPower               enableLiftPower             = 0x52,
  RawPWM                        setRawPWM                   = 0x53,
  SetMotionModelParams          setMotionModelParams        = 0x54,
  CubeSlots                     assignCubeSlots             = 0x55,
  
  // 0x80 to 0xAF: To WiFi
  // Flash and OTA message must not be changed or OTA will break
  EraseFlash                    eraseFlash                  = 0x80,
  WriteFlash                    writeFlash                  = 0x81,
  OTAUpgrade                    triggerOTAUpgrade           = 0x82,
  NVStorage::NVStorageBlob      writeNV                     = 0x83,
  NVStorage::NVStorageRead      readNV                      = 0x84,
  // Reserved for FlashWriteAcknowledge message in OTA      = 0x85,
  BodyFirmwareState             bodyState                   = 0x86,
  RTIPVersionInfo               rtipVersion                 = 0x87,
  AbortAnimation                abortAnimation              = 0x90,
  AnimKeyFrame::AudioSample     animAudioSample             = 0x91,
  AnimKeyFrame::AudioSilence    animAudioSilence            = 0x92,
  AnimKeyFrame::HeadAngle       animHeadAngle               = 0x93,
  AnimKeyFrame::LiftHeight      animLiftHeight              = 0x94,
  AnimKeyFrame::FacePosition    animFacePosition            = 0x95,
  AnimKeyFrame::Blink           animBlink                   = 0x96,
  AnimKeyFrame::FaceImage       animFaceImage               = 0x97,
  AnimKeyFrame::BackpackLights  animBackpackLights          = 0x98,
  AnimKeyFrame::BodyMotion      animBodyMotion              = 0x99,
  AnimKeyFrame::EndOfAnimation  animEndOfAnimation          = 0x9A,
  AnimKeyFrame::StartOfAnimation animStartOfAnimation       = 0x9B,
  // Anim future expansion                                  = 0x9C,
  AnimKeyFrame::DisableAnimTracks disableAnimTracks         = 0x9D,
  AnimKeyFrame::EnableAnimTracks enableAnimTracks           = 0x9E,
  // Anim future expansion                                  = 0x9F,

  // 0xB0 to 0xFE: To engine, see messageRobotToEngine.clad
}

} // namespace Robot Interface
} // namespace Cozmo
} // namespace Anki
